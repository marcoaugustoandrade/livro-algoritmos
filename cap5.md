# Cap√≠tulo 5 - Fun√ß√µes
A medida que desenvolvemos nossos algoritmos fica evidente que o c√≥digo cresce, com dezenas ou centenas de linhas de c√≥digo, e entender ou modific√°-lo fica cada vez mais dif√≠cil.

Outra situa√ß√£o √© que come√ßamos a repetir muito c√≥digo. Imagine que voc√™ queira realizar uma verifica√ß√£o se um determinado CPF √© v√°lido ou inv√°lido. O c√≥digo para realizar essa verifica√ß√£o pode ser um tanto complexo ou at√© mesmo grande. Assim, utilizar o recurso `copiar` e `colar` sempre que precisar realizar tal verifica√ß√£o talvez n√£o seja uma boa op√ß√£o.

Para melhorar nosso c√≥digo vamos recorrer ao conceito de modulariza√ß√£o e utilizar fun√ß√µes para organizar nosso c√≥digo. Fun√ß√µes s√£o blocos de c√≥digo que podem ser reutilizados nos nossos algoritmos. Voc√™ est√° escrevendo seu c√≥digo dentro da fun√ß√£o `in√≠cio` desde que come√ßou a escrever seus algoritmos no Portugol Studio üòØÔ∏è. Essa √© uma fun√ß√£o especial que √© executada sempre que o algoritmo √© executado.

As fun√ß√µes, no Portugol Studio, s√£o declaradas precedidas da palavra `funcao`, e retornam valores, que podem ser `cadeia`, `caracter`, `inteiro`, `l√≥gico` ou `real`, e podem receber par√¢metros entre os par√™nteses. Estes par√¢metros, s√£o vari√°veis que poder√£o ser manipuladas pela fun√ß√£o. Observe o c√≥digo a seguir:
{% highlight portugol %}
programa
{
	
	funcao real calcularDescontoPrevidencia(real salario)
	{
		real desconto = (salario / 100) * 11
		retorne desconto
	}

	funcao inicio(){
		real salario = 1800.00
		real descontoPrevidencia = calcularDescontoPrevidencia(salario)
		escreva("Com um sal√°rio de " + salario + ", haver√° um desconto de " + descontoPrevidencia)
    }
}
{% endhighlight %}

N√≥s declaramos um vari√°vel `salario` com o valor 1800.00 (normalmente voc√™ vai pedir para o usu√°rio digitar o valor da sal√°rio, mas simplificamos para o entendimento), e queremos saber qual ser√° o desconto da Previd√™ncia Social.

Assim, criamos uma `descontoPrevidencia`, do tipo `real`, que recebe o `retorno` da fun√ß√£o `calcularDescontoPrevidencia`, que tamb√©m retorna um valor do tipo `real`.

At√© aqui identificamos que uma fun√ß√£o retorna valores de um tipo, pode receber par√¢metros e ser executada em qualquer parte do c√≥digo, e at√© dentro dela mesmo (mas √© assunto para o pr√≥ximo cap√≠tulo).

No exemplo a seguir, modificamos a fun√ß√£o `calcularDescontoPrevidencia` para receber dois par√¢metros: o sal√°rio e a quantidade de dependentes, e com isso calcular qual ser√° o desconto.
{% highlight portugol %}
programa
{
	
	funcao real calcularDescontoPrevidencia(real salario, inteiro dependentes)
	{
		real desconto = 0.00
		se (dependentes < 2){
			desconto = (salario / 100) * 11
		} senao {
			desconto = (salario / 100) * 10
		}
		retorne desconto
	}

	funcao inicio(){
		real salario = 1800.00
		inteiro dependentes = 2
		real descontoPrevidencia = calcularDescontoPrevidencia(salario, dependentes)
		escreva("Com um sal√°rio de " + salario + ", haver√° um desconto de " + descontoPrevidencia)
	}
}
{% endhighlight %}

Uma fun√ß√£o pode n√£o ter retorno, ou seja, ter um retorno `vazio`, e normalmente voc√™ pode encontrar esses tipos de fun√ß√µes nomeadas como `procedimentos`. No exemplo a seguir, vamos criar e utilizar uma fun√ß√£o que imprime se o aluno √© maior ou menor de idade:
{% highlight portugol %}
programa
{

	funcao vazio maioridade(inteiro idade)
	{
		se (idade >= 18){
			escreva("√â maior de idade")
		} senao {
			escreva("√â menor de idade")
		}
	}
	
	funcao inicio()
	{
		maioridade(17)
	}
}
{% endhighlight %}

Observe que a fun√ß√£o n√£o retornou um valor, mas imprimiu no console se o aluno √© maior ou menor de idade.

## Passagem de par√¢metros
At√© o momento os par√¢metros informados em uma fun√ß√£o s√£o passados como uma c√≥pia, ou seja, caso haja modifica√ß√£o de seus valores n√£o √© alterado o valor da vari√°vel original. Essa forma √© definida como `passagem de par√¢metros por valor`. Vamos utilizar o exemplo a seguir para enteder melhor esse conceito:
{% highlight portugol %}
programa
{

	funcao real precoFinal(real preco)
	{
		preco = preco * 1.5
		retorne preco
	}
	
	funcao inicio()
	{
		real preco = 100.0
		escreva("Pre√ßo do produto: " + preco)
		escreva("Pre√ßo final do produto: " + precoFinal(preco))
		escreva("Pre√ßo do produto: " + preco)
	}
}
{% endhighlight %}

Na fun√ß√£o in√≠cio √© declarada uma vari√°vel preco, do tipo `real`, que recebe o valor 100.00. Em seguida, imprimimos no console o seu valor e, obviamente, ser√° impresso o valor 100.0.

Em seguida, queremos imprimir o pre√ßo final de um produto, que √© calculado pela fun√ß√£o `precoFinal` que recebe como par√¢metro um vari√°vel `preco` do tipo `real`. Dentro desta fun√ß√£o, modificamos o valor do preco, multiplicando por 1.5 e retornamos seu valor. E a partir do uso desta fun√ß√£o imprimimos o pre√ßo final que √© 150.0.

Por fim, imprimimos novamente o pre√ßo, que retorna o valor de 100.0.

Voc√™ deve estar se perguntando, mas eu n√£o modifiquei o valor da vari√°vel `preco`.

A resposta √© n√£o!

A vari√°vel `real preco = 100.0` declarada dentro da fun√ß√£o in√≠cio √© uma coisa, a vari√°vel `real preco` que √© o par√¢metro da fun√ß√£o `precoFinal` √© outra coisa.


Entrentanto, h√° casos em que √© necess√°rio que os par√¢metros sejam as vari√°veis de fato. Assim, utilizamos a `passagem de par√¢metros por refer√™ncia`. A √∫nica diferen√ßa √© que utilizamos o `&` no par√¢metro da fun√ß√£o, veja:
{% highlight portugol %}
programa
{

	funcao real precoFinal(real &preco)
	{
		preco = preco * 1.5
		retorne preco
	}
	
	funcao inicio()
	{
		real preco = 100.0
		escreva("Pre√ßo do produto: " + preco)
		escreva("Pre√ßo final do produto: " + precoFinal(preco))
		escreva("Pre√ßo do produto: " + preco)
	}
}
{% endhighlight %}

Qual ser√£o os valores impressos? 100.0, 150.0 e 150, respectivamente, pois ap√≥s a execu√ß√£o da fun√ß√£o `precoFinal` o valor da vari√°vel `preco` foi alterado.

Tamb√©m podemos passar um vetor ou matriz como par√¢metro de uma fun√ß√£o. O detalhe √© que esse valor ser√° passado, no caso do Portugol Stuio, sempre por *refer√™ncia*, e n√£o precisamos utilizar o *&* para simbolizar.
{% highlight portugol %}
programa
{

	funcao vazio incrementarIdade(inteiro idades[])
	{
		para (inteiro i = 0; i < 5; i++){
			idades[i] = idades[i] + 1
		}
	}
	
	funcao inicio()
	{
		inteiro idades[5] = {10, 12, 10, 11, 9}
		escreva("Imprimindo os valores do vetor:\n")
		para (inteiro i = 0; i < 5; i++){
			escreva(idades[i] + "\n")
		}

		// Utilizando a fun√ß√£o incrementarIdade para alterar, por refer√™ncia os valores do vetor
		incrementarIdade(idades)
		escreva("Imprimindo os novos valores do vetor:\n")
		para (inteiro i = 0; i < 5; i++){
			escreva(idades[i] + "\n")
		}
	}
}
{% endhighlight %}
