# Cap√≠tulo 1 - Escrevendo um algoritmo
Voc√™ precisa trocar uma l√¢mpada queimada em sua casa. Quais s√£o os passos necess√°rios para realizar esta troca?

Poder√≠amos pensar na seguinte sequ√™ncia de passos: 1) pegar a escada; 2) colocar a escada abaixo do bocal; 3) subir na escada; 4) remover a l√¢mpada queimada do bocal; 5) colocar a nova l√¢mpada no bocal; 6) descer da escada e 7) testar a nova l√¢mpada apertando o interruptor.

Pode ser que voc√™ tenha pensado num passo-a-passo um pouco diferente do meu, e tudo bem com isso. Problemas similares s√£o resolvidos de formas distintas por cada pessoa.

Assim, podemos dizer que os algoritmos s√£o uma sequ√™ncia de passos para resolver um problema, e n√≥s fazemos isso todos os dias. A diferen√ßa aqui √© que vamos utilizar um computador e a pseudo-linguagem Portugol (Portugu√™s Estruturado) para criar algoritmos.

Se sua inten√ß√£o √© ser um programador n√£o se preocupe. Se conseguir resolver problemas criando algoritmos com o Portugol, ser√° f√°cil aprender uma linguagem de programa√ß√£o como Java, C, Python, JavaScript, etc.

A primeira atividade √© instalar o [Portugol Studio](http://lite.acad.univali.br/portugol/). Esse ambiente vai nos ajudar nessa jornada para escrevermos nosso primeiro algoritmo.

Os subt√≥picos deste cap√≠tulo s√£o os seguintes:
* [Vari√°veis](#variaveis)
* [Estrutura b√°sica de um algoritmo](#estrutura-basica-de-um-algoritmo)
* [Declarando uma vari√°vel](#declarando-uma-variavel)
* [Modificando o conte√∫do de uma vari√°vel](#modificando-o-conteudo-de-uma-variavel)
* [Escrevendo no console](#escrevendo-no-console)
* [Operadores de atribui√ß√£o](#operadores-de-atribuicao)
* [Operadores aritm√©ticos](#operadores-aritmeticos)
* [Lendo dados do usu√°rio](#lendo-dados-do-usuario)
* [Constantes](#constantes)
* [Teste de mesa](#teste-de-mesa)


## Vari√°veis
Um dos prop√≥sitos de um programa de computador √© manipular dados. Esses dados podem ser a idade de uma pessoa, seu nome, seu sexo, seu sal√°rio se ele pagou ou n√£o a conta de energia, e assim por diante. Com esses dados podemos realizar opera√ß√µes, como calcular se o aluno √© maior de idade, apresentar ao usu√°rio do programa um relat√≥rio contendo os endere√ßo para entrega de um produto, com base em seu tempo de contribui√ß√£o calcular quanto tempo falta para aposentadoria e assim por diante. Para **guardar** esses dados o programa de computador utiliza o conceito de vari√°veis. A imagem a seguir ilustra a mem√≥ria de computador como uma caixa dividida em compartimentos:

![Representa√ß√£o da mem√≥ria de um computador](assets/images/memoria.png)

Em cada compartimento desse voc√™ pode guardar uma quantidade de dados, e para cada tipo de dado (explica√ß√£o a seguir), √© utilizado uma quantidade de desses compartimentos.

Cada **dado** pode ser classificado por um tipo, vejamos:
* Para guardar a quantidade de alunos precisamos de uma vari√°vel do tipo **inteiro**, pois s√≥ temos dados do tipo num√©rico inteiro, 0 (zero) alunos, 1 (um) aluno, 2 (dois) alunos;
* Para guardar o sal√°rio de um funcion√°rio precisamos de uma vari√°vel do tipo **real**, pois temos valores com casas decimais, assim um funcion√°rio poderia ter o sal√°rio de 1.200,60 (mil duzentos reais e sessenta centavos);
* Para guardar o sexo de um funcion√°rio precisamos de vari√°vel do tipo **caracter**, pois podemos simplificar e dizer que 'F' representar√° o sexo feminino e 'M' representar√° o sexo masculino;
* Para guardar o nome de um aluno precisamos de mais que um caracter. Assim, utilizamos o tipo **cadeia** (de caracteres): "Em√≠lia Andrade", "Mariana Oliveira";
* Para guardar o dado que uma conta de energia foi paga podemos utilizar o tipo **logico**, que representa apenas dois estados: verdadeiro ou falso;

A tabela a seguir representa os tipos b√°sicos de dados que podemos utilizar no Portugol.
<table class="table">
  <tr>
    <th>Tipo</th>
    <th>Explica√ß√£o</th>
    <th>Exemplo</th>
  </tr>
  <tr>
    <td>Real</td>
    <td>valores num√©ricos reais</td>
    <td>18.39</td>
  </tr>
  <tr>
    <td>Inteiro</td>
    <td>valores num√©ricos inteiros</td>
    <td>10</td>
  </tr>
  <tr>
    <td>Caracter</td>
    <td>um caracter</td>
    <td>A</td>
  </tr>
  <tr>
    <td>Cadeia</td>
    <td>um conjunto de caracteres</td>
    <td>Rua das flores n¬∫ 1023</td>
  </tr>
  <tr>
    <td>L√≥gico</td>
    <td>pode assumir apenas verdadeiro ou falso</td>
    <td>verdadeiro</td>
  </tr>  
</table>

Ent√£o, se for necess√°rio guardar a quantidade de parcelas de um ve√≠culo qual ser√° o tipo de dado mais adequado?

ü§î Pense um pouco...

Devemos utilizar o tipo inteiro, pois a quantidade de parcelas √© um n√∫mero inteiro. Podemos ter 0 (zero) parcelas, podemos ter 2 (duas) parcelas, 30 (trinta) parcelas, mas n√£o teremos meia parcela.

Agora responda qual o tipo de dado adequado para as seguintes situa√ß√µes:
* O pre√ßo de um produto
* A quantidade de um determinado produto
* A descri√ß√£o de um produto
* Se o aluno foi aprovado ou reprovado
* Armazenar a temperatura em Celsius
* Armazenar a temperatura em Fahrenheit
* Uma letra do alfabeto
* O endere√ßo de um funcion√°rio

Respostas: << <a onclick="show('#var_tipo', this)">mostrar respostas</a>
<ul id="var_tipo" style="display: none">
	<li> Para guardar o pre√ßo de um produto precisamos de uma vari√°vel do tipo real, pois um produto pode custar R$ 10,50 (dez reais e cinquenta centatos).</li>
	<li> Para guardar a quantidade de um produto temos uma pegadinha, pois depende de que tipo de produto. Imagine que o produto vendido ser√£o carros. Nesse caso podemos utilizar o tipo inteiro, pois n√£o venderemos meio carro. Agora imagine que o produto vendido √© carne, e nesse caso, podemos vender meio quilo de carne, e nesse caso o tipo de dado deve ser real.</li>
	<li> Para guardar a descri√ß√£o de um produto precisamos de mais de um caracter, sendo mais adequado o tipo cadeia.</li>
	<li> Para guardar a informa√ß√£o que o aluno foi aprovado ou reprovado podemos utilizar o tipo l√≥gico, pois teremos somente 2 situa√ß√µes, convencionando que verdadeiro ser√° quando ele for aprovado e falso quando ele for reprovado.</li>
	<li> Para guardar as temperaturas precisamos de uma vari√°vel do tipo real, que serve para Celsius e Fahrenheit.</li>
	<li> Para guardar somente uma letra do alfabeto precisamos de uma vari√°vel do tipo caracter.</li>
	<li> Para guardar o endere√ßo de um funcion√°rio precisamos de v√°rios caracteres, assim, o tipo que devemos utilizar √© o cadeia.</li>
</ul>


## Estrutura b√°sica de um algoritmo
Abra o Portugol Studio e crie um novo arquivo. Observe que o algoritmo criado j√° possui uma estrutura, existe uma palavra **programa** e abaixo dela √© aberta uma '{' chave que √© fechada no final do algoritmo. Observe tamb√©m que h√° uma **funcao inicio()** que, da mesma forma, tem abaixo uma '{' chave que √© fechada em seguida. Essas chaves representam o escopo e t√™m a fun√ß√£o de limitar o que est√° dentro. Assim, visualmente √© poss√≠vel perceber que a **funcao inicio()** est√° dentro do **programa**.
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		// O seu c√≥digo ser√° digitado aqui		
	}
}
{% endhighlight %}

<div class="message-warning">Cada instru√ß√£o (comando) √© escrito em uma linha.</div>

Por ora voc√™ deve compreender que todo o c√≥digo que iremos digitar ficar√° dentro da fun√ß√£o **inicio**, que nada mais √© uma fun√ß√£o que √© executada quando o programa √© executado.

Outra coisa que precisamos esclarecer nesse momento s√£o os coment√°rios dentro do nosso c√≥digo. Perceba que na linha 5 utilizamos duas barras '//' com o texto **O seu c√≥digo ser√° digitado aqui**. Essas duas barras indicam que a linha √© um coment√°rio e n√£o ser√° executado pelo Portugol Studio. Esse recurso √© bem √∫til para auxiliar no entendimento do c√≥digo descrevendo algo que seja de entendimento complexo.


## Declarando uma vari√°vel
Agora que voc√™ j√° sabe o que √© uma vari√°vel e conhece a estrutura b√°sica de um algoritmo vamos aprender a declar√°-las, ou seja, disponibiliza-las para que possam receber conte√∫do. Dentro da fun√ß√£o inicio vamos declarar algumas vari√°veis:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		cadeia nome
		cadeia endereco
		caracter sexo_do_aluno
		inteiro idade
		real salario
		logico realizado
	}
}
{% endhighlight %}

Por conven√ß√£o vamos nomear as vari√°veis iniciando com uma letra min√∫scula e contendo somente letras (mai√∫sculas ou min√∫sculas), n√∫meros ou os caracteres _ (underline) ou - (tra√ßo). Tamb√©m √© importante utilizar um nome que identifique o seu conte√∫do. **salario** √© mais adequado para uma vari√°vel para armazenar um sal√°rio do que **slrio**, por exemplo.

Qual dos nomes a seguir s√£o corretos para nomear vari√°veis. Caso n√£o seja defina um novo nome:
a) nome do cliente  
b) Salario  
c) IDADE  
d) idade  
e) nomeDaM√£e  
f) 1telefone  
g) 2telefone  
h) Cargo  

Respostas: << <a onclick="show('#var_nomear', this)">mostrar respostas</a>
<ul id="var_nomear" style="display: none">
	<li>a) nome-do-cliente ou nome_do_cliente ou nomeDoCliente</li>
	<li>b) salario</li>
	<li>c) idade</li>
	<li>d) Nome adequado!</li>
	<li>e) nomeDaMae</li>
	<li>f) telefone1</li>
	<li>g) telefone2</li>
	<li>h) cargo</li>
</ul>

<div class="message-info"> Existem alguns estilos para nomea√ß√£o de vari√°veis e fun√ß√µes. Com o CamelCase podemos criar nomes de vari√°veis com palavras compostas, onde a letra inicial de cada palavra √© mai√∫scula, como por exemplo: SalarioBruto, SalarioLiquido e NomeDaMae. Uma forma de empregar o CamelCase em nomes de vari√°veis e fun√ß√µes √© iniciar com a primeira palavra min√∫scula, veja: salarioBruto, salarioLiquido e nomeDaMae. Com o snake_case utilizamos o caracter underline para criar nomes de vari√°veis e fun√ß√µes com palavras compostas, ficando assim: salario_bruto, salario_liquido e nome_da_mae</div>

<div class="message-danger">Outro aspecto importante a se considerar √© o **case sensitive**: a maioria das linguagens diferencia mai√∫sculas e min√∫sculas. Assim, idade √© diferente de Idade ou IDADE ou iDaDe. </div>


## Modificando o conte√∫do de uma vari√°vel
Ap√≥s declarar uma vari√°vel provavelmente voc√™ vai querer modificar o conte√∫do dela (que a princ√≠pio n√£o tem nada). Para fazermos isso basta chamar o nome da vari√°vel e atribuir um conte√∫do:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		cadeia nome
		nome = "Maria da Silva"

		caracter sexo
		sexo = 'F'

		inteiro idade
		idade = 12

		real salario
		salario = 2400.45

		logico realizado
		realizado = verdadeiro
	}
}
{% endhighlight %}

Vamos ler o que est√° codificado:
* Linha 5: √© declarada uma vari√°vel nome do tipo cadeia. A vari√°vel nome **recebe** o valor Maria da Silva na linha 6.
* Linha 8: √© declarada uma vari√°vel sexo do tipo caracter. A vari√°vel sexo **recebe** o valor F na linha 10.
* Linha 10: √© declarada uma vari√°vel idade do tipo inteiro. A vari√°vel idade **recebe** o valor 12 na linha 11.
* Linha 13: √© declarada uma vari√°vel salario do tipo real. A vari√°vel salario **recebe** o valor 2400.45 na linha 14.
* Linha 16: √© declarada uma vari√°vel realizado do tipo logico. A vari√°vel realizado **recebe** o valor verdadeiro na linha 17.

Mas perceberam que o conte√∫do "Maria da Silva" da vari√°vel nome est√° entre aspas duplas? Pois bem, dependendo do tipo de vari√°vel o conte√∫do deve seguir uma norma, vejamos:

* Para vari√°veis do tipo **cadeia** o conte√∫do dever√° estar entre **aspas duplas**.
* Para vari√°veis do tipo **caracter** colocamos o caracter entre **aspas simples**.
* Para vari√°veis do tipo **inteiro** basta atribuir um **valor num√©rico inteiro**.
* Para vari√°veis do tipo **real** atribu√≠mos um valor do tipo real, mas precisamos nos atentar que as **casas decimais s√£o separadas por pontos**.
* Para vari√°veis do tipo **logico** s√≥ temos dois valores poss√≠veis: **verdadeiro** ou **falso**.

Em determinadas situa√ß√µes al√©m de declarar a vari√°vel, e assim reservar um espa√ßo na mem√≥ria para ela, necessitamos atribuir um valor imediatamente. Imagine que voc√™ est√° criando um programa para uma conta banc√°ria e quer que o saldo inicialmente seja 0 (zero) e o limite seja 1.000 (mil).
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		cadeia nome_do_cliente
		real saldo = 0.0
		real limite = 1000.00
	}
}
{% endhighlight %}

Veja que a variavel saldo foi declarada e j√° recebeu o valor 0, e a vari√°vel limite foi declarada com o valor 1.000. Enquanto a vari√°vel nome_do_cliente foi declarada mas n√£o recebeu nenhum valor.

Atribua valores as seguintes vari√°veis:
a) cadeia nome =  
b) inteiro qtd =  
c) logico realizado =  
d) caracter sexo =  
e) real salario =  
f) cadeia cpf =  
g) inteiro cpf =  
h) real qtd =  
i) cadeia endereco =  
j) real valor_unitario =  
k) real valor_total =  
l) inteiro idade =  

Respostas: << <a onclick="show('#var_atribuir', this)">mostrar respostas</a>
<ul id="var_atribuir" style="display: none">
	<li>a) nome = "Em√≠lia Andrade"</li>
	<li>b) qtd = 10</li>
	<li>c) realizado = falso</li>
	<li>d) sexo = 'M'</li>
	<li>e) salario = 2450.00</li>
	<li>f) cpf = "450.432.456-90"</li>
	<li>g) cpf = 45043245690</li>
	<li>h) qtd = 15.4</li>
	<li>i) endereco = "Avenida do Contorno n¬∫ 1010"</li>
	<li>j) valor_unitario = 13.20</li>
	<li>k) valor_total = 30.00</li>
	<li>l) idade = 18</li>
</div>


## Escrevendo no console
O Portugol Studio tem uma √°rea para visualizarmos a sa√≠da da execu√ß√£o dos nossos algoritmos chamada de console. Se voc√™ executar o algoritmo a seguir o que ir√° acontecer?
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		inteiro idade
		idade = 12
	}
}
{% endhighlight %}

A vari√°vel idade foi declarada e, em seguida, teve 12 como valor atribu√≠do. O resultado da execu√ß√£o no console foi algo assim:
{% highlight portugol %}
Programa finalizado. Tempo de execu√ß√£o: 3 milissegundos.
{% endhighlight %}

Mas na maioria dos casos voc√™ vai querer que o algoritmo informe ao usu√°rio alguma informa√ß√£o, certo? No caso do nosso algoritmo queremos que a idade declarada seja impressa. Para isso vamos utilizar o comando **escreva()**:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		inteiro idade
		idade = 12
		escreva(idade)
	}
}
{% endhighlight %}

A sa√≠da do console ser√°:  
{% highlight portugol %}
12
{% endhighlight %}

Mas 12 o que? N√£o √© muito informativo, certo?

Pois bem, al√©m de utilizar o comando escreva para imprimir o conte√∫do de uma vari√°vel podemos utiliza-lo para escrever algum texto informativo, e **concatena-lo** com vari√°veis utilizando o operador **+**:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		inteiro idade
		idade = 12
		escreva("Programa de c√°lculo")
		escreva("A idade do aluno √© " + idade)
	}
}
{% endhighlight %}

Agora a sa√≠da do console ser√° a seguinte:  
{% highlight portugol %}
Programa de c√°lculo
A idade do aluno √© 12
{% endhighlight %}

Na linha 8 al√©m do texto informativo, que √© uma **cadeia**, e assim precisamos coloca-lo entre aspas duplas, estamos concatenando o valor da vari√°vel idade, que √© 12. Agora fica claro para o usu√°rio do que se trata o programa.


## Operadores de atribui√ß√£o
Quando uma vari√°vel **recebe** um valor com o operador **=** estamos fazendo uma atribui√ß√£o. H√° outras opera√ß√µes de atribui√ß√£o, como o incremento e o decremento:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		inteiro idade = 12
		idade++
		escreva(idade)

		inteiro contador = 10
		contador--
		escreva(idade)
	}
}
{% endhighlight %}

<div class="message-warning">
Na linha 6 o conte√∫do da vari√°vel idade foi incremento em 1, passando a valer 13. Na linha 10 o conte√∫do da vari√°vel contador foi decrementando em 1, passando a valer 9.
</div>

Em outras palavras estamos dizendo que: a idade recebe o pr√≥prio valor mais um; o contador recebe o pr√≥prio valor menos um. O c√≥digo a seguir faz a mesma coisa:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		inteiro idade = 12
		idade = idade + 1
		escreva(idade)

		inteiro contador = 10
		contador = contador - 1
		escreva(contador)
	}
}
{% endhighlight %}

Tamb√©m podemos utilizar atribui√ß√µes para simplificar nosso c√≥digo. Imagine que o sal√°rio foi reajustado em R$ 200,00:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		real salario = 1400.00
		salario += 200.00
	}
}
{% endhighlight %}

A leitura da linha 6 √© que a vari√°vel sal√°rio foi incrementada em 200. Uma outra forma de fazer isso seria assim:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		real salario = 1400.00
		salario = salario + 200.00
	}
}
{% endhighlight %}

Agora precisamos dar desconto de 5% no pre√ßo de um produto:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		real preco = 30
		preco -= (preco * 0.05)
	}
}
{% endhighlight %}

Isso √© equivalente a:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		real preco = 30
		preco = preco - (preco * 0.05)
	}
}
{% endhighlight %}


## Operadores aritm√©ticos
Com as vari√°veis do tipo inteiro e real podemos realizar as opera√ß√µes aritm√©ticas b√°sicas: somar, subtrair, multiplar e dividir.
<table>
	<tr>
		<th>Opera√ß√£o</th>
		<th>S√≠mbolo</th>
	</tr>
	<tr>
		<td>adi√ß√£o</td>		
		<td>+</td>
	</tr>
	<tr>
		<td>subtra√ß√£o</td>
		<td>-</td>
	</tr>
	<tr>
		<td>multiplica√ß√£o</td>
		<td>*</td>
	</tr>
	<tr>
		<td>divis√£o</td>
		<td>/</td>
	</tr>
</table>

Assim como na matem√°tica as opera√ß√µes de divis√£o e multiplica√ß√£o tem prioridade sobre as opera√ß√µes de somar e substrair.  
6 + 2 * 3  
Ser√° executada a multiplica√ß√£o 2 * 3 e o seu resultado ser√° somado com 6, com valor final de 12.

Tamb√©m podemos utilizamos par√™nteses para modifica√ß√£o da ordem das opera√ß√µes:  
(6 + 2) * 3  
Primeiro ser√° realiza a soma 6 + 2 e o seu resultado ser√° multiplicado por 3, com valor final de 24.

Na matem√°tica colocamos mais n√≠veis atrav√©s de '[' colchetes e '{' chaves. Aqui n√≥s utilizamos os par√™nteses:  
(((6 + 2) * 3) / 2)  
Primeiro ser√° realiza soma 6 + 2, resultando em 8, que ser√° multiplicado por 3, resultando em 24, que ent√£o ser√° divido por 2, resultando em 12.

As variav√©is podem receber, al√©m de valores de outras vari√°veis, express√µes artim√©ticas:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		real salario
		real vale_alimentacao = 250.00
		
		salario = 2350.50
		salario = salario + vale_alimentacao
		vale_alimentacao = 250.00 * 1.2
	}
}
{% endhighlight %}

Uma vari√°vel **salario** foi declarada como tipo real. Tamb√©m declaramos uma vari√°vel **vale_alimentacao** recebendo o valor 250.00. Em seguida, a vari√°vel salario recebeu o valor 2350.50. Finalmente, a vari√°vel salario recebeu o pr√≥rio conte√∫do, ou seja, 2350.50 somando com o conte√∫do da vari√°vel vale_alimentacao (250.00), resultando em 2600.50. Por fim, reajustamos o valor da vari√°vel vale_alimentacao em 20%.


## Lendo dados do usu√°rio
Pode ser que voc√™ queira que o pr√≥prio usu√°rio informe os valores de uma vari√°vel. Faz sentido? Imagine que voc√™ est√° desenvolvendo um algoritmo para que o valor de uma conta na lanchonete seja dividido em partes iguais entre seus amigos. Al√©m do valor da conta, precisamos da quantidade de amigos. Para ler valores informados pelo usu√°rio utilizamos o comando **leia()**, veja:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		inteiro quantidade_amigos
		real valor_conta
		real valor_por_amigo

		escreva("Informe o valor da conta: ")
		leia(valor_conta)
		
		escreva("Informe a quantidade de amigos: ")
		leia(quantidade_amigos)
		
		valor_por_amigo = valor_conta / quantidade_amigos
		escreva("Cada amigo dever√° pagar R$ " + valor_por_amigo)
	}
}
{% endhighlight %}

Observe nas linhas 10 e 13 utilizamos o comando **leia()** para ler algum valor que o usu√°rio do programa ir√° digitar e, em seguida, atribuir esse valor a vari√°vel informada ao comando. Na linha 15 realizamos o c√°lculo do valor por amigo dividindo o totalda conta pelo quantidade de amigos, e na linha 16 imprimimos quanto cada amigo dever√° pagar.


## Constantes
H√° determinados dados que n√£o devem mudar durante a execu√ß√£o de um algoritmo. O n√∫mero pi tem o valor 3,1415 e n√£o muda. Ou voc√™ pode determinar que um determinado dado n√£o muda durante a execu√ß√£o do algoritmo. Para garantir que estes valores n√£o alterem durante a execu√ß√£o do algoritmo os declaramos como constantes:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		const real PI = 3.1415
	}
}
{% endhighlight %}

<div class="message-danger">Por conven√ß√£o declaramos as constantes em caixa alta (todas as letras mai√∫sculas).</div>

Uma vari√°vel pode recebe o valor de uma constante. O algoritmo a seguir calcula a √°rea do c√≠rculo com base no valor do raio informado pelo usu√°rio e pelo n√∫mero pi:
{% highlight portugol %}
programa
{
	funcao inicio()
	{
		const real PI = 3.1415
		real raio
		real area

		escreva("Informe o raio do c√≠rculo: ")
		leia(raio)

		area = PI * (raio * raio)

		escreva("A √°rea do c√≠rculo √© " + area)		
	}
}
{% endhighlight %}


## Teste de mesa
Uma forma de verificar o que est√° acontecendo com os valores das vari√°veis durante a execu√ß√£o do algoritmo √© utilizando um teste de mesa. Voc√™ pode faz√™-lo em uma folha de caderno, mas no nosso caso, o Portugol Studio disponibiliza uma √°rea para isso.
<!-- Colocar um GIF -->
![]()

<script src="assets/js/script.js"></script>
